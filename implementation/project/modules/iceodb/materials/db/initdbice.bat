@echo off
echo.

rem ----------------------------------------------------------------------------
rem
rem Скрипт создает БД ЛЁД в СУБД PostgreSQL.
rem Если БД ЛЁД уже существует в PostgreSQL,
rem скрипт перед созданием удалит БД ЛЁД.
rem
rem Скрипту передаются параметры в следующем порядке:
rem
rem	1. Путь к папке bin установленной директории СУБД PostgreSQL
rem    (Если в пути есть пробелы, путь необходимо заключить в двойные ковычки)
rem	2. Пользователь PostgreSQL
rem	3. Пароль пользователя PostgreSQL
rem 4. Имя базы данных
rem
rem
rem Возвращаемое значение:
rem 
rem	1. 0 - БД ЛЁД создана и инициализирована успешно
rem	2. 1 - Ошибка создания и ининциализации БД ЛЁД
rem
rem
rem ----------------------------------------------------------------------------
rem ВНИМАНИЕ:
rem
rem	Проверка на корректность передаваемых параметров 
rem	не производится, все они должны быть правильные
rem ----------------------------------------------------------------------------


rem Это переменная окружения PostgreSQL
set PGPASSWORD=%3

rem Удаляем БД ЛЁД, если ее нет, ничего страшного
call dropdbice %1 %2 %3 %4 

rem Создаем БД ЛЁД
%1\createdb -EWIN1251 -O%2 -Ttemplate0 -U%2 %4 || goto FAILED

rem Накатываем скрипты создания структуры БД ЛЁД
for /F %%A in (sqls.txt) do (
  %1\psql -d%4 -U%2 -f../../iceodb/%%A || goto FAILED
)

rem Накатываем исходные данные БД ЛЁД
call setdbicetestdata.bat %1 %2 %3 %4 

goto SUCCESS

:FAILED
  echo.
  echo ____________
  echo %~nx0
  echo FAILED!!!
  color C7

  exit /B 1


:SUCCESS
  echo.
  echo ____________
  echo %~nx0
  echo SUCCESS!!!
  color 07

  exit /B 0
