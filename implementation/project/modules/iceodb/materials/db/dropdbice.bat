@echo off
echo.

rem ----------------------------------------------------------------------------
rem
rem Скрипт удаления БД ЛЁД
rem
rem Скрипту передаются параметры в следующем порядке:
rem
rem	1. Путь к папке bin установленной директории СУБД PostgreSQL
rem    (Если в пути есть пробелы, путь необходимо заключить в двойные ковычки)
rem	2. Пользователь PostgreSQL
rem	3. Пароль пользователя PostgreSQL
rem 4. Имя базы данных
rem
rem
rem Возвращаемое значение:
rem
rem	1. 0 - БД ЛЁД удалена успешно (или не было необходимости ее удалять, 
rem	т.к. ее не было)
rem	2. 1 - Ошибка удаления БД ЛЁД
rem
rem ----------------------------------------------------------------------------
rem ВНИМАНИЕ:
rem
rem	Проверка на корректность передаваемых параметров 
rem	не производится, все они должны быть правильные
rem ----------------------------------------------------------------------------

rem Это переменная окпужения PostgreSQL
set PGPASSWORD=%3

rem Проверяем, существует ли БД ЛЁД в СУБД
rem Если ее не существует, то и удалять ее не нужно, считаем, что процесс 
rem удаления прошел корректно
call isexistdbice %1 %2 %3 %4 || goto SUCCESS

rem Удаляем БД ЛЁД
%1\dropdb -U%2 %4 || goto FAILED

goto SUCCESS

:FAILED
  echo.
  echo ____________
  echo %~nx0
  echo FAILED!!!
  color C7

  exit /B 1


:SUCCESS
  echo.
  echo ____________
  echo %~nx0
  echo SUCCESS!!!
  color 07

  exit /B 0